plugins {
    id 'war'
}

dependencies {
    // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.1.0'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.18.1'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.1'

    // https://mvnrepository.com/artifact/com.github.jknack/handlebars
    implementation group: 'com.github.jknack', name: 'handlebars', version: '4.4.0'


    implementation project(':krim2244-backend')
}

// def deploymentName = 'krim2244'
def deploymentName = 'krim2244-web'
def tomcatHome = System.getenv('CATALINA_HOME')
logger.info("Tomcat home detected as '${tomcatHome}'")

task deploy(type: Copy, dependsOn: 'war') {
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    from war.archiveFile
    rename war.archiveFileName.get(), "${deploymentName}.war"
    into "${tomcatHome}/webapps"
}

// Undeploy (delete) the war from Tomcat
task undeploy(type: Delete) {
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    delete "${tomcatHome}/webapps/${deploymentName}.war"
}
